<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="FB_GetRtMode" Id="{4d4213e6-afe1-480d-842c-2b5a73469412}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_GetRtMode
VAR_INPUT	
	bExecute			: BOOL;(* Rising edge at this input starts command execution *)
	tTimeout			: TIME:=T#5S;
END_VAR
VAR_OUTPUT
	bBusy				: BOOL;
	bError				: BOOL;
	nErrId				: UDINT;
	rtMode				: E_RtMode;
END_VAR
VAR
	state 				: UDINT;
	RisingEdge			: R_TRIG;	
	_rtMode				: UDINT;
	fbAdsRead			: ADSREAD := ( NETID := '', PORT := AMSPORT_R0_RTIME, IDXGRP := 1 (*RTADSGRP_SYSDATA*), IDXOFFS:=16#32 (* RTADSOFFS_SYSDATA_TICK_MODE*) );	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[RisingEdge( CLK:= bExecute );
CASE state OF
	0:
		IF RisingEdge.Q THEN
			(* reset outputs *)
			bBusy := TRUE;
			bError := FALSE;
			nErrId := 0;
			
			(* calculate the required source buffer byte size *)
			fbAdsRead( READ := FALSE );
			(* start command execution *)
			fbAdsRead( LEN := SIZEOF(_rtMode), DESTADDR:= ADR(_rtMode), READ := TRUE, TMOUT := tTimeout );
			(* go to the wait state *)
			state := 1;			
		END_IF
	1:(* wait until fb not busy *)		
		fbAdsRead( READ := FALSE, BUSY=>bBusy, ERR=>bError, ERRID=>nErrID );
		IF NOT fbAdsRead.BUSY THEN
			IF NOT fbAdsRead.ERR THEN
				CASE _rtMode OF
					E_RtMode.RtMode_FastAsPossible:
						rtMode:=E_RtMode.RtMode_FastAsPossible;
					E_RtMode.RtMode_Cyclic:
						rtMode:=E_RtMode.RtMode_Cyclic;
					E_RtMode.RtMode_Externaltick:
						rtMode:=E_RtMode.RtMode_Externaltick;
				ELSE
					rtMode:= E_RtMode.RtMode_Invalid;
				END_CASE
			END_IF
			state := 0;		 
		END_IF
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="FB_GetRtMode">
      <LineId Id="34" Count="7" />
      <LineId Id="72" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="53" Count="2" />
      <LineId Id="58" Count="1" />
      <LineId Id="65" Count="3" />
      <LineId Id="129" Count="1" />
      <LineId Id="132" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="134" Count="0" />
      <LineId Id="136" Count="4" />
      <LineId Id="133" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="69" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>